set shell := ["bash", "-euo", "pipefail", "-c"]

_default:
	@just --list 

mod install 'install.just'
mod remove 'remove.just'
mod config 'config.just'

# Generates a dynamic inventory string for Ansible.
_node_inv HOSTS:
	{ V="{{HOSTS}}"; case "$V" in *=*) V="${V#*=}";; esac; case "$V" in *","*) printf "%s\n" "$V";; "") echo "HOSTS is required" >&2; exit 2;; *) printf "%s\n" "${V},";; esac; }

# Core engine to run any Ansible playbook.
_run_playbook PLAYBOOK HOSTS *ARGS:
  { \
   B="{{justfile_directory()}}"; \
   ANSIBLE_CONFIG="$B/ansible.cfg" \
   ANSIBLE_ROLES_PATH="$B/roles" \
    ansible-playbook \
      -i "$B/inventory/hosts.yml" \
      --limit "{{ HOSTS }}" \
      {{ARGS}} \
     "$B/playbooks/{{PLAYBOOK}}.yml"; \
  }
